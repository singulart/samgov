plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.gradleup.shadow' version '9.3.1'
	id 'maven-publish'
}

group = 'com.argorand'
version = '0.4.2-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

sourceSets {
	main {
		java { srcDirs = ['notifier/src/main/java'] }
		resources { srcDirs = ['notifier/src/main/resources'] }
	}
	test {
		java { srcDirs = ['notifier/src/test/java'] }
		resources { srcDirs = ['notifier/src/test/resources'] }
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2025.1.1")
}

assemble.dependsOn shadowJar

import com.github.jengelman.gradle.plugins.shadow.transformers.*

shadowJar {
	archiveClassifier = 'aws'
	manifest {
		inheritFrom(project.tasks.bootJar.manifest)
	}
	dependencies {
		exclude(dependency('org.springframework.cloud:spring-cloud-function-web'))
	}
	// Allow append transformers to receive duplicate files (default EXCLUDE prevents merging)
	filesMatching('META-INF/spring/**') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	filesMatching('META-INF/spring.factories') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	filesMatching('META-INF/spring.handlers') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	filesMatching('META-INF/spring.schemas') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	filesMatching('META-INF/spring.tooling') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	filesMatching('META-INF/spring.components') {
		duplicatesStrategy = DuplicatesStrategy.INCLUDE
	}
	mergeServiceFiles()
	append 'META-INF/spring.handlers'
	append 'META-INF/spring.schemas'
	append 'META-INF/spring.tooling'
	append 'META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
	append 'META-INF/spring/org.springframework.boot.actuate.autoconfigure.web.ManagementContextConfiguration.imports'
	append 'META-INF/spring.components'
	transform(PropertiesFileTransformer) {
		paths = ['META-INF/spring.factories']
		mergeStrategy = "append"
	}
}

dependencies {
	implementation 'software.amazon.awssdk:dynamodb-enhanced:2.41.33'
	implementation 'software.amazon.awssdk:ses:2.41.33'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

configurations {
	compileClasspath {
		resolutionStrategy.activateDependencyLocking()
	}
	runtimeClasspath {
		resolutionStrategy.activateDependencyLocking()
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			versionMapping {
				usage('java-api') { fromResolutionOf('runtimeClasspath') }
				usage('java-runtime') { fromResolutionResult() }
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
