plugins {
	// id 'org.springframework.cloud.function.aws-lambda.packaging' version '1.0.0'
	id 'java'
	id 'org.springframework.boot' version '4.0.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.gradleup.shadow' version '9.3.1'
	id 'maven-publish'
}

group = 'com.argorand'
version = '0.3.0-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('springCloudVersion', "2025.1.1")
}

assemble.dependsOn shadowJar

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}

import com.github.jengelman.gradle.plugins.shadow.transformers.*;

shadowJar {
	archiveClassifier = 'aws'
	manifest {
 	  	inheritFrom(project.tasks.bootJar.manifest)
  	}
  	// Required for Spring
	mergeServiceFiles()
	append 'META-INF/spring.handlers'
	append 'META-INF/spring.schemas'
	append 'META-INF/spring.tooling'
	append 'META-INF/spring/org.springframework.boot.autoconfigure.AutoConfiguration.imports'
	append 'META-INF/spring/org.springframework.boot.actuate.autoconfigure.web.ManagementContextConfiguration.imports'
	transform(PropertiesFileTransformer) {
		paths = ['META-INF/spring.factories']
		mergeStrategy = "append"
	}

	// This is un-successful attempt to minimize the jar. 

	// minimize {
	// 	exclude(dependency('org.springframework.cloud:spring-cloud-function-adapter-aws:.*'))
	// 	exclude(dependency('commons-logging:.*:.*'))
	// }
}

dependencies {
	implementation 'software.amazon.awssdk:dynamodb-enhanced:2.41.33'
	implementation platform("tools.jackson:jackson-bom:3.0.0")
	implementation 'software.amazon.awssdk:ses:2.41.33'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.cloud:spring-cloud-function-adapter-aws'
	implementation "tools.jackson.core:jackson-databind"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

tasks.named('test') {
	useJUnitPlatform()
}